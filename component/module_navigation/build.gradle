if (!includeModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
                arguments = [moduleName: project.getName(), AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            //默认的作为application运行时Manifest文件路径
            if (!includeModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下自动排除debug文件夹中的所有Java文件
                // 可以将debug代码放在这个包内，例如：Application子类
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api project(":lib")
    annotationProcessor project(':apt')

    //compiler没有传递关系，虽然在:lib中注册了
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc1'

    implementation "com.google.code.gson:gson:2.8.5"
    implementation "qiyi.basecore.network:networklib:10.3.5.2"

    api 'com.facebook.fresco:fresco:1.10.0'
    // 支持 GIF 动图，需要添加
    api 'com.facebook.fresco:animated-gif:1.10.0'
    // 支持 WebP （静态图+动图），需要添加
    api 'com.facebook.fresco:animated-webp:1.10.0'
    api 'com.facebook.fresco:webpsupport:1.10.0'
}
